// XP SCRIPTS

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,1249999)
  XPLT(Myself,1250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1250000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,999999)
  XPLT(Myself,1000000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1000000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,749999)
  XPLT(Myself,750000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,750000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,499999)
  XPLT(Myself,500000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,500000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,249999)
  XPLT(Myself,250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,250000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,124999)
  XPLT(Myself,125000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,125000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,100001)
  XPLT(Myself,100000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,100000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPLT(Player1,89001)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    SetInterrupt(TRUE)
END

//

IF
Global("L#2SkieStartFirstPQ","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("L#2SkieStartFirstPQ","LOCALS",1)
StartDialogueNoSet(Player1)
END

/////////////////////////////////////
/////////////////////////////////////
////////////TIMERED TALKS////////////
/////////////////////////////////////
/////////////////////////////////////

IF
InParty(Myself)
Global("L#2SDSkieFriendshipActive","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2SDSkieFriendshipActive","GLOBAL",1)
END

IF
Global("L#2SDSkieMean","GLOBAL",3)
OR(2)
Global("L#2SDSkieFriendshipActive","GLOBAL",1)
Global("L#2SDSkieFriendshipActive","GLOBAL",2)
THEN 
RESPONSE #100
SetGlobal("L#2SDSkieFriendshipActive","GLOBAL",3)
END

IF
InParty(Myself)
OR(2)
Global("L#2SDSkieFriendshipActive","GLOBAL",1)
Global("L#2SDSkieFriendshipActive","GLOBAL",2)
OR(6)
Global("L#2SDSkieTalks","GLOBAL",0)
Global("L#2SDSkieTalks","GLOBAL",3)
Global("L#2SDSkieTalks","GLOBAL",6)
Global("L#2SDSkieTalks","GLOBAL",9)
Global("L#2SDSkieTalks","GLOBAL",12)
Global("L#2SDSkieTalks","GLOBAL",15)
THEN
RESPONSE #100
RealSetGlobalTimer("L#2SDSkieTimer","GLOBAL",2400)
IncrementGlobal("L#2SDSkieTalks","GLOBAL",1)
END

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
RealGlobalTimerExpired("L#2SDSkieTimer","GLOBAL")
OR(2)
Global("L#2SDSkieFriendshipActive","GLOBAL",1)
Global("L#2SDSkieFriendshipActive","GLOBAL",2)
OR(5)
Global("L#2SDSkieTalks","GLOBAL",1)
Global("L#2SDSkieTalks","GLOBAL",4)
Global("L#2SDSkieTalks","GLOBAL",7)
Global("L#2SDSkieTalks","GLOBAL",10)
Global("L#2SDSkieTalks","GLOBAL",13)
THEN
RESPONSE #100
IncrementGlobal("L#2SDSkieTalks","GLOBAL",1)
END

IF
InParty(Myself)
OR(2)
Global("L#2SDSkieFriendshipActive","GLOBAL",1)
Global("L#2SDSkieFriendshipActive","GLOBAL",2)
OR(7)
Global("L#2SDSkieTalks","GLOBAL",2)
Global("L#2SDSkieTalks","GLOBAL",5)
Global("L#2SDSkieTalks","GLOBAL",8)
Global("L#2SDSkieTalks","GLOBAL",11)
Global("L#2SDSkieTalks","GLOBAL",14)
Global("L#2SDSkieTalks","GLOBAL",17)
Global("L#2SDSkieTalks","GLOBAL",18)
THEN
RESPONSE #100
MoveToObject(Player1)
PlaySong(%L#2SDSK%)
StartDialogue("L#2SDSKJ",Player1)
END

IF
InParty(Myself)
Global("L#SDOneTimeComparison","LOCALS",0)
THEN
RESPONSE #100
RealSetGlobalTimer("L#2SDSkieComparisonTime","GLOBAL",8000)
SetGlobal("L#SDOneTimeComparison","LOCALS",1)
END

////////////////////////////////////
///////AFTER FRIENDSHIP TALKS///////
////////////////////////////////////

IF
InParty(Myself)
Global("L#2SDMissiniReturns","GLOBAL",0)
OR(2)
Global("L#2SDSkieFriendshipActive","GLOBAL",2)
Global("L#2SDSkieFriendshipActive","GLOBAL",3)
THEN
RESPONSE #100
RealSetGlobalTimer("L#2SDMissini2Time","GLOBAL",600)
SetGlobal("L#2SDMissiniReturns","GLOBAL",1)
END

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
Global("L#2SDMissiniReturns","GLOBAL",1)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
RealGlobalTimerExpired("L#2SDMissini2Time","GLOBAL")
THEN
RESPONSE #100
StartCutSceneMode()
Wait(1)
CreateCreatureObjectOffset("L#2SDMES",Player1,[40.40])
SmallWait(1)
CreateVisualEffectObject("SPDIMNDR","l#2sdmes")
Wait(5)
SetGlobal("L#2SDMissiniReturns","GLOBAL",2)
Wait(1)
EndCutSceneMode()
ActionOverride("L#2SDMES",StartDialogueNoSet(Player1))
END

IF
InParty(Myself)
Global("L#2SDMissiniReturns","GLOBAL",3)
Global("L#2SDCommentAfter","GLOBAL",0)
THEN
RESPONSE #100
StartDialogue("L#2SDSKJ",Player1)
END

/////////////////////////////////
////////SITUATIONAL TALKS////////
/////////////////////////////////


//BEHOLDER LAIR or UNSEEING EYE LAIR

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
OR(2)
AreaCheck("AR0205")
AreaCheck("AR2101")
Global("L#2SDSkieMeat","GLOBAL",0)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("L#2SDSkieMeat","GLOBAL",1)
END

IF
Global("L#2SDSkieMeat","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END

//RADIANT HEART

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR0903")
Global("L#2SDSkieRadiant","GLOBAL",0)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("L#2SDSkieRadiant","GLOBAL",1)
END

IF
Global("L#2SDSkieRadiant","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END


//SOULTAKER RESTORED

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
Global("L#2SDSkieSoultakerRes","GLOBAL",0)
OR(3)
PartyHasItem("l#2sdag1")
PartyHasItem("l#2sdag2")
PartyHasItem("l#2sdag3")
!GlobalTimerExpired("L#SDTalkTimerForST","GLOBAL")
THEN
RESPONSE #100
SetGlobal("L#2SDSkieSoultakerRes","GLOBAL",1)
END

IF
Global("L#2SDSkieSoultakerRes","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END

//UNDERWATER CITY

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR2300")
Global("L#2SDSkieUnderwater","GLOBAL",0)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
RealSetGlobalTimer("L#SDSkieUnderwTime","GLOBAL",400)
SetGlobal("L#2SDSkieUnderwater","GLOBAL",1)
END

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR2300")
RealGlobalTimerExpired("L#SDSkieUnderwTime","GLOBAL")
Global("L#2SDSkieUnderwater","GLOBAL",1)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("L#2SDSkieUnderwater","GLOBAL",2)
END

IF
Global("L#2SDSkieUnderwater","GLOBAL",2)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END

//REPUTATION (POST-MISSINI)

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
ReputationLT(Player1,8)
Global("L#2SDSkieWarns","GLOBAL",1)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("L#2SDSkieWarns","GLOBAL",2)
END

IF
Global("L#2SDSkieWarns","GLOBAL",2)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END

//POST-SLAYER

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR1508")
Global("L#2SDSkiePostSlayer","GLOBAL",0)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
RealSetGlobalTimer("L#SDSkiePSTime","GLOBAL",180)
SetGlobal("L#2SDSkiePostSlayer","GLOBAL",1)
END

IF
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
RealGlobalTimerExpired("L#SDSkiePSTime","GLOBAL")
Global("L#2SDSkiePostSlayer","GLOBAL",1)
!StateCheck("L#2SDSkie",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("L#2SDSkiePostSlayer","GLOBAL",2)
END

IF
Global("L#2SDSkiePostSlayer","GLOBAL",2)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END

//ELDOTH PT.1

IF
GlobalGT("L#2SDSkieEldothReact","GLOBAL",0)
GlobalLT("L#2SDSkieEldothReact","GLOBAL",4)
Global("L2SDEldothPlot","GLOBAL",2)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END

//ELDOTH PT.2

IF
Dead("L#2SDELD")
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
Global("L2SDEldothPlot","GLOBAL",5)
THEN
RESPONSE #100
SetGlobal("L2SDEldothPlot","GLOBAL",6)
END

IF
Global("L2SDEldothPlot","GLOBAL",6)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2SDSKJ",Player1)
END