// XP SCRIPTS

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,1249999)
  XPLT(Myself,1250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1250000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,999999)
  XPLT(Myself,1000000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1000000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,749999)
  XPLT(Myself,750000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,750000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,499999)
  XPLT(Myself,500000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,500000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,249999)
  XPLT(Myself,250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,250000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,124999)
  XPLT(Myself,125000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,125000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,100001)
  XPLT(Myself,100000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,100000,SET)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPLT(Player1,89001)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    SetInterrupt(TRUE)
END


/////////////////////////////////////
/////////////////////////////////////
////////////TIMERED TALKS////////////
/////////////////////////////////////
/////////////////////////////////////

//FRIENDSHIP

IF
InParty(Myself)
Global("L#2EddardFriendshipActive","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardFriendshipActive","GLOBAL",1)
END

IF
InParty(Myself)
Global("L#2EddardFriendshipActive","GLOBAL",1)
OR(7)
 Global("L#2EddardFriendTalk","GLOBAL",0)
 Global("L#2EddardFriendTalk","GLOBAL",3)
 Global("L#2EddardFriendTalk","GLOBAL",6)
 Global("L#2EddardFriendTalk","GLOBAL",12)
 Global("L#2EddardFriendTalk","GLOBAL",15)
 Global("L#2EddardFriendTalk","GLOBAL",18)
 Global("L#2EddardFriendTalk","GLOBAL",21)
THEN
RESPONSE #100
RealSetGlobalTimer("L#2EddardFriendTalkTime","GLOBAL",2400)
IncrementGlobal("L#2EddardFriendTalk","GLOBAL",1)
END

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!AreaType(DUNGEON)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
RealGlobalTimerExpired("L#2EddardFriendTalkTime","GLOBAL")
Global("L#2EddardFriendshipActive","GLOBAL",1)
OR(6)
 Global("L#2EddardFriendTalk","GLOBAL",1)
 Global("L#2EddardFriendTalk","GLOBAL",4)
 Global("L#2EddardFriendTalk","GLOBAL",13)
 Global("L#2EddardFriendTalk","GLOBAL",16)
 Global("L#2EddardFriendTalk","GLOBAL",19)
 Global("L#2EddardFriendTalk","GLOBAL",22)
THEN
RESPONSE #100
IncrementGlobal("L#2EddardFriendTalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("L#2EddardFriendshipActive","GLOBAL",1)
OR(8)
 Global("L#2EddardFriendTalk","GLOBAL",2)
 Global("L#2EddardFriendTalk","GLOBAL",5)
 Global("L#2EddardFriendTalk","GLOBAL",8)
 Global("L#2EddardFriendTalk","GLOBAL",11)
 Global("L#2EddardFriendTalk","GLOBAL",14)
 Global("L#2EddardFriendTalk","GLOBAL",17)
 Global("L#2EddardFriendTalk","GLOBAL",20)
 Global("L#2EddardFriendTalk","GLOBAL",23)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//MISSINI

IF
Global("L#2EddardFriendTalk","GLOBAL",9)
THEN
RESPONSE #100
StartCutSceneMode()
Wait(1)
CreateCreatureObjectOffset("L#2SDMES",Player1,[40.40])
SmallWait(1)
CreateVisualEffectObject("SPDIMNDR","l#2sdmes")
MoveViewObject(Myself,INSTANT)
Wait(5)
SetGlobal("L#2EddardFriendTalk","GLOBAL",10)
Wait(1)
EndCutSceneMode()
ActionOverride("L#2SDMES",StartDialogueNoSet(Player1))
END

//ROMANCE

IF
InParty(Myself)
Global("L#2EddardMatch","GLOBAL",1)
Global("L#2EddardFriendshipActive","GLOBAL",2)
Global("L#2EddardRomanceActive","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardRomanceActive","GLOBAL",1)
END

IF
InParty(Myself)
OR(2)
Global("L#2EddardRomanceActive","GLOBAL",1)
Global("L#2EddardRomanceActive","GLOBAL",2)
OR(13)
 Global("L#2EddardRomTalk","GLOBAL",0)
 Global("L#2EddardRomTalk","GLOBAL",3)
 Global("L#2EddardRomTalk","GLOBAL",6)
 Global("L#2EddardRomTalk","GLOBAL",9)
 Global("L#2EddardRomTalk","GLOBAL",12)
 Global("L#2EddardRomTalk","GLOBAL",15)
 Global("L#2EddardRomTalk","GLOBAL",18)
 Global("L#2EddardRomTalk","GLOBAL",21)
 Global("L#2EddardRomTalk","GLOBAL",24)
 Global("L#2EddardRomTalk","GLOBAL",27)
 Global("L#2EddardRomTalk","GLOBAL",30)
 Global("L#2EddardRomTalk","GLOBAL",34)
 Global("L#2EddardRomTalk","GLOBAL",37)
THEN
RESPONSE #100
RealSetGlobalTimer("L#2EddardRomTalkTime","GLOBAL",2400)
IncrementGlobal("L#2EddardRomTalk","GLOBAL",1)
END

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!AreaType(DUNGEON)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
RealGlobalTimerExpired("L#2EddardRomTalkTime","GLOBAL")
OR(2)
Global("L#2EddardRomanceActive","GLOBAL",1)
Global("L#2EddardRomanceActive","GLOBAL",2)
OR(9)
 Global("L#2EddardRomTalk","GLOBAL",1)
 Global("L#2EddardRomTalk","GLOBAL",4)
 Global("L#2EddardRomTalk","GLOBAL",10)
 Global("L#2EddardRomTalk","GLOBAL",13)
 Global("L#2EddardRomTalk","GLOBAL",22)
 Global("L#2EddardRomTalk","GLOBAL",25)
 Global("L#2EddardRomTalk","GLOBAL",28)
 Global("L#2EddardRomTalk","GLOBAL",35)
 Global("L#2EddardRomTalk","GLOBAL",38)
THEN
RESPONSE #100
IncrementGlobal("L#2EddardRomTalk","GLOBAL",1)
END

IF
InParty(Myself)
OR(2)
Global("L#2EddardRomanceActive","GLOBAL",1)
Global("L#2EddardRomanceActive","GLOBAL",2)
OR(14)
 Global("L#2EddardRomTalk","GLOBAL",2)
 Global("L#2EddardRomTalk","GLOBAL",5)
 Global("L#2EddardRomTalk","GLOBAL",8)
 Global("L#2EddardRomTalk","GLOBAL",11)
 Global("L#2EddardRomTalk","GLOBAL",14)
 Global("L#2EddardRomTalk","GLOBAL",17)
 Global("L#2EddardRomTalk","GLOBAL",20)
 Global("L#2EddardRomTalk","GLOBAL",23)
 Global("L#2EddardRomTalk","GLOBAL",26)
 Global("L#2EddardRomTalk","GLOBAL",29)
 Global("L#2EddardRomTalk","GLOBAL",32)
 Global("L#2EddardRomTalk","GLOBAL",33)
 Global("L#2EddardRomTalk","GLOBAL",36)
 Global("L#2EddardRomTalk","GLOBAL",39)
THEN
RESPONSE #100
MoveToObject(Player1)
PlaySong(%L#2EDDR%)
StartDialogue("L#2EDJ",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!AreaType(DUNGEON)
PartyRested()
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
RealGlobalTimerExpired("L#2EddardRomTalkTime","GLOBAL")
OR(2)
 Global("L#2EddardRomanceActive","GLOBAL",1)
 Global("L#2EddardRomanceActive","GLOBAL",2)
Global("L#2EddardRomTalk","GLOBAL",19)
THEN
RESPONSE #100
IncrementGlobal("L#2EddardRomTalk","GLOBAL",1)
END

//ROMANCE KILL

IF
OR(2)
Global("L#2EddardRomanceActive","GLOBAL",1)
Global("L#2EddardRomanceActive","GLOBAL",2)
OR(44)
Global("AerieRomanceActive","GLOBAL",2)        //Aerie
Global("JaheiraRomanceActive","GLOBAL",2)      //Jaheira
Global("AnomenRomanceActive","GLOBAL",2)       //Anomen
Global("ViconiaRomanceActive","GLOBAL",2)      //Viconia
Global("NeeraRomanceActive","GLOBAL",2)        //Neera
Global("RasaadRomanceActive","GLOBAL",2)       //Rasaad
Global("HexxatRomanceActive","GLOBAL",2)       //Hexxat
Global("DornRomanceActive","GLOBAL",2)         //Dorn
Global("ImoenRomanceActive","GLOBAL",2)        //Mirrabo's Imoen
Global("NaliaRomanceActive","GLOBAL",2)        //Arnel's Nalia
Global("PGNaliaRomanceActive","GLOBAL",2)      //Pocket Plane Group's Nalia
Global("NathRomanceActive","GLOBAL",2)         //Nathaniel
Global("LK#NindeRomanceActive","GLOBAL",2)     //Ninde
Global("SaerilethRomanceActive","GLOBAL",2)    //Saerileth
Global("SimeRomanceActive","GLOBAL",2)         //Sime
GlobalGT("SolaTalk","GLOBAL",14)               //Solaufein
Global("TashiaRomanceActive","GLOBAL",2)       //Tashia
Global("G#TF.TyrisRomanceActive","GLOBAL",2)   //Tyris Flare
Global("E3FADEROMANCEACTIVE","GLOBAL",2)       //Fade
Global("O#BranRomanceActive","GLOBAL",2)       //Branwen
Global("rh#IsraRomanceActive","GLOBAL",2)      //Isra
Global("L3PetsyRomanceActive","GLOBAL",2)      //Petsy 
Global("C0SireneRomanceActive","GLOBAL",2)     //Sirene
Global("L#2VerrRomanceActive","GLOBAL",2)      //Verr'Sza
Global("YvetteRomanceActive","GLOBAL",2)       //Yvette
Global("YoshimoRomanceActive","GLOBAL",2)      //Yoshimo
Global("C#AjantisRomanceActive","GLOBAL",2)    //Ajantis
Global("ChloeRomanceActiveCR","GLOBAL",2)      //Chloe
Global("T1DarRomanceActive","GLOBAL",2)        //Darian
Global("B!GavRA","GLOBAL",2)                   //Gavin
Global("B!KeldornRA","GLOBAL",2)               //Keldorn
Global("J#KelseyRomanceActive","GLOBAL",2)     //Kelsey
Global("SK#NehtRomanceActive","GLOBAL",2)      //Neh'taniel
Global("SarahRomanceActive","GLOBAL",2)        //Sarah
Global("TsujathaRomanceActive","GLOBAL",2)     //Tsujatha
Global("O#XanRomanceActive","GLOBAL",2)        //Xan 
Global("rh#EvaRomanceActive","GLOBAL",2)       //Evendra
Global("P#KivanRomanceInactive","GLOBAL",2)    //Kivan
Global("EdwinRomanceActive","GLOBAL",2)        //Edwin
Global("KKCalinRomanceActive","GLOBAL",2)      //Calin
Global("LS#SarevokRomanceActive","GLOBAL",2)   //Sarevok
Global("LS_HaerDalisRomanceActive","GLOBAL",2) //Haer'Dalis
Global("L#2WalahnanFlirt","GLOBAL",2)          //Walahnan
Global("L#FaldornRomanceActive","GLOBAL",2)    //Faldorn
THEN
RESPONSE #100
SetGlobal("L#2EddardRomanceActive","GLOBAL",3)
END

///////////////////////////////////
///////////// SCENERY /////////////
///////////////////////////////////

//RADIANT HEART

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR0903")
Range("Statue1",25)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardRadiantHeart","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardRadiantHeart","GLOBAL",1)
END

IF
Global("L#2EddardRadiantHeart","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//COPPER CORONET

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR0406")
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardCopperC","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardCopperC","GLOBAL",1)
END

IF
Global("L#2EddardCopperC","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//PLANAR

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR0516")
PartyHasItem("MISC7x")
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardThrallsOrb","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardThrallsOrb","GLOBAL",1)
END

IF
Global("L#2EddardThrallsOrb","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//DRUNK

IF
Global("L#2EddardDrunk","GLOBAL",0)
InParty(Myself)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CheckStatGT("L#2EDDRD",30,INTOXICATION)
OR(16)
AreaCheck("AR0021")
AreaCheck("AR0313")
AreaCheck("AR0314")
AreaCheck("AR0406")
AreaCheck("AR0509")
AreaCheck("AR0513")
AreaCheck("AR0514")
AreaCheck("AR0515")
AreaCheck("AR0522")
AreaCheck("AR0704")
AreaCheck("AR0709")
AreaCheck("AR0712")
AreaCheck("AR1105")
AreaCheck("AR2010")
AreaCheck("l#nd07")
AreaCheck("L#ni05")
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("L#2EddardDrunk","GLOBAL",1)
END

IF
Global("L#2EddardDrunk","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardDrunk","GLOBAL",2)
CheckStatLT("L#2EDDRD",10,INTOXICATION)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("L#2EddardDrunk","GLOBAL",3)
END

IF
Global("L#2EddardDrunk","GLOBAL",3)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//UNDERDARK

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR2100")
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardUD1","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("L#2EddardUD1Time","GLOBAL",800)
SetGlobal("L#2EddardUD1","GLOBAL",1)
END

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
GlobalTimerExpired("L#2EddardUD1Time","GLOBAL")
Global("L#2EddardUD1","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("L#2EddardUD1","GLOBAL",2)
END

IF
Global("L#2EddardUD1","GLOBAL",2)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//UNDERDARK2

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
AreaCheck("AR2400")
OR(4)
 Range("GLOBE1",15)
 Range("GLOBE2",15)
 Range("GLOBE6",15)
 Range("GLOBE4",15)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("FreeOrDeadSlave","AR2400",0)
Global("L#2EddardUD2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardUD2","GLOBAL",1)
END

IF
Global("L#2EddardUD2","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//LANTHORN

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
PartyHasItem("c6lantho")
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
!Global("L#2EddardRomanceActive","GLOBAL",1)
!Global("L#2EddardRomanceActive","GLOBAL",2)
Global("L#2EddardLanthorn","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardLanthorn","GLOBAL",1)
END

IF
Global("L#2EddardLanthorn","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//BRACERS

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
PartyHasItem("L#2SDBRC")
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardBracers","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardBracers","GLOBAL",1)
END

IF
Global("L#2EddardBracers","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//SLAYER 

IF
InParty(Myself)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardScenerySlayer","GLOBAL",0)
AreaCheck("AR1514")
GlobalGT("AsylumPlot","GLOBAL",42)
THEN
RESPONSE #100
SetGlobal("L#2EddardScenerySlayer","GLOBAL",1)
END

IF
Global("L#2EddardScenerySlayer","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//VAMPIRE

IF
AreaCheck("AR1401")
OR(2)
 Global("L#2EddardVampire","GLOBAL",3)
 Global("L#2EddardVampire","GLOBAL",4)
THEN
RESPONSE #100
SetGlobal("L#2EddardVampire","GLOBAL",4)
StartDialogueNoSet(Player1)
END
 
//BOOK / GIFT

IF
InParty(Myself)
PartyHasItem("L#2EDBOO")
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck("L#2EDDRD",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("L#2EddardBook","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardBook","GLOBAL",1)
END

IF
Global("L#2EddardBook","GLOBAL",1)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogue("L#2EDJ",Player1)
END

//ELDOTH COMMENTS

IF
InParty(Myself)
InParty("L#2SDSkie")
AreaCheck("AR1602")
Global("L2SDEldothPlot","GLOBAL",4)
Global("L#2EddardEldothCom1","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardEldothCom1","GLOBAL",1)
END

IF
Global("L#2EddardEldothCom1","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
InParty("L#2SDSkie")
AreaCheck("AR1600")
Global("L2SDEldothPlot","GLOBAL",7)
Global("L#2EddardEldothCom2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardEldothCom2","GLOBAL",1)
END

IF
Global("L#2EddardEldothCom2","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//PHAERE

IF
InParty(Myself)
Global("PhaereInnuendo","GLOBAL",2)
Global("L#2EddardRomanceActive","GLOBAL",2)
Global("L#2EddardCommentPhaereSex","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardCommentPhaereSex","GLOBAL",1)
END

IF
Global("L#2EddardCommentPhaereSex","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//SKIE DEAD

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!InPartyAllowDead("L#2SDSKIE")
Dead("L#2SDSKIE")
Global("L#2EddardBlockEverything","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("L#2EddardBlockEverything","GLOBAL",1)
END

IF
Global("L#2EddardBlockEverything","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
Global("L#2EddardBlockEverything","GLOBAL",2)
OR(2)
 Global("L#2EddardRomanceActive","GLOBAL",1)
 Global("L#2EddardRomanceActive","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("L#2EddardRomanceActive","GLOBAL",3)
END
 
IF
Global("L#2EddardBlockEverything","GLOBAL",2)
OR(2)
 Global("L#2EddardFriendshipActive","GLOBAL",1)
 Global("L#2EddardFriendshipActive","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("L#2EddardFriendshipActive","GLOBAL",3)
END